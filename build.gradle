apply plugin: 'com.android.library'

repositories {
    google()
    mavenCentral()
    mavenLocal()
    maven {
        url 'http://4thline.org/m2'
    }
    maven { url 'https://jitpack.io' }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            //tmp prebuilt jni
            jniLibs.srcDirs = ['libs']
            //disable automatic jni build rules
            jni.srcDirs = []
        }
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
    }

    task ndkBuild(type:Exec) {
        workingDir = "../"
        List<String> myCmd = new ArrayList<String>()
        myCmd.add("make")
        myCmd.add("native_libnativehelper")
        myCmd.add("native_build_FileCoreLibrary")
        commandLine myCmd
    }

    task ndkClean(type:Exec) {
        workingDir = "../"
        List<String> myCmd = new ArrayList<String>()
        myCmd.add("make")
        myCmd.add("native_clean")
        commandLine myCmd
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
    clean.dependsOn 'ndkClean'

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    api files('libs/jcifs-ng-2.1.0-SNAPSHOT.jar')
    // jcifs-ng depends on bouncycastle (automated when using maven repo)
    api  'org.bouncycastle:bcprov-jdk15on:1.58'
    //api 'eu.agno3.jcifs:jcifs-ng:2.0.4'
    implementation 'oro:oro:2.0.8'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.linkedin.dexmaker:dexmaker:2.12.1'
}
